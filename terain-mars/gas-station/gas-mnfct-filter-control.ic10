define STRUCTURExSIZE 6
define SHIFT 10
define COUNTxADDRESS 9
alias Switch r0
alias Filter r1 
alias Banner r2
alias Bypass r3
alias LargeFilter r4
alias MediumFilter r5
alias AutoSwitch r6
alias Count r7
get Count db COUNTxADDRESS
alias idx r8
alias dataAddres r9
alias PWR r10
alias BannerColor r11
alias FilterState r12 #1 - operable, 0 - failed
alias AutoRequest r13 #0 - no request, -1 request to stop, 1 request to start
lbn AutoSwitch HASH("ModularDeviceSwitch") HASH("gw-auto-sw") ReferenceId Sum
s $62791 Mode DisplayMode.Pa #Auto display 
s $62791 Color Color.Gray
move sp 0
main:
    yield
    l PWR $5F25B On #PWR
    beqz PWR shutdown
    jal chekFilters
j main 
chekFilters:
    move idx 0
    move dataAddres Count
    mul dataAddres dataAddres STRUCTURExSIZE
    add dataAddres dataAddres SHIFT #Shift
cycleFilters:
    jal read
    bnezal PWR checkAuto
    bnezal PWR checkFiltersState
    jal selectSwitchState
    l r15 Switch On
    s Filter On r15
    seqz r15 r15
    and r15 r15 PWR
    s Bypass On r15
    add idx idx 1
    blt idx Count cycleFilters
j main
checkAuto:
    move AutoRequest 0
    l r15 $62790 Setting  #Pressure limit
    mul r15 r15 100000
    s $62791 Setting r15  #Store limit for DSP 
    l r15 AutoSwitch On
    select r14 r15 Color.Blue Color.Red
    s AutoSwitch Color r14
    beqz r15 ra
    l r15 $62790 Setting  #Pressure limit
    mul r14 r15 100
    l r15 $5D68F Pressure #Mix tank pressure
    sgt AutoRequest r15 r14
    l r14 Filter On
    or AutoRequest AutoRequest r14
    seqz r14 r15
    select AutoRequest r14 -1 AutoRequest
j ra
read:
    move r15 sp
    move sp dataAddres
    pop Filter
    pop MediumFilter
    pop LargeFilter
    pop Bypass
    pop Banner
    pop Switch
    move dataAddres sp
    move sp r15
j ra
checkFiltersState:
  ls r15 Filter 0 Quantity
  ls r14 Filter 1 Quantity
  add r14 r15 r14
  sgt r15 r14 100
  select BannerColor r15 Color.Blue Color.Orange
  seqz r15 r14
  select BannerColor r15 Color.Red BannerColor
  push ra
  move r15 0
  jal checkFalseFilter
  select BannerColor r15 BannerColor Color.Red
  breqz r15 4 #Banner is red
  move r15 1
  jal checkFalseFilter
  select BannerColor r15 BannerColor Color.Red
  pop ra
  s Banner Color BannerColor
  seq r15 BannerColor Color.Red
  seqz FilterState r15
  s Banner Mode r15
j ra
checkFalseFilter:
  ls r14 Filter r15 Occupied
  beqz r14 goodFilter
  ls r14 Filter r15 OccupantHash
  beq r14 LargeFilter goodFilter
  beq r14 MediumFilter goodFilter
  move r15 0
  j ra
goodFilter:
  move r15 1
j ra
selectSwitchState:
    move r15 0
    breqz FilterState 6
    breqz PWR 5
    brnez AutoRequest 3
        l r15 Switch On
    jr 2
        seq r15 AutoRequest 1 
    s Switch On r15 
    select r15 r15 Color.Green Color.Red
    s Switch Color r15  
j ra
shutdown:
    move idx 0
    move dataAddres Count
    mul dataAddres dataAddres STRUCTURExSIZE
    add dataAddres dataAddres SHIFT #Shift
    push ra
shutdownCycle:
    jal read
    add idx idx 1
    s Filter On 0
    s Bypass On 0
blt idx Count shutdownCycle
    l PWR $5F25B On #PWR
    s db On PWR #Shutdown self
j main
