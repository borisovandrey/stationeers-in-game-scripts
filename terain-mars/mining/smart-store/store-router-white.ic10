#terain-mars\mining\smart-store\store-router-white.ic10
#13.11.2025 andrey.borisov@gmx.de 
#Color: white
#Application uses FlipFlop chuts for route input to selected output
#Output is set as db.Setting as 1,2...max exits
alias Router db
alias OldRoute r0
alias Route r1
alias SelectedId r2
alias Address r3
define PASS 0 # When set to Mode, cause direct pass
define EXTRACT 1 # When set to Mode, cause side exit
define INIT -1
define Exits HASH("StructureChuteDigitalFlipFlopSplitterRight")

push 0
lbn r15 Exits HASH("router-exit-1") ReferenceId Sum 
push r15
lbn r15 Exits HASH("router-exit-2") ReferenceId Sum 
push r15
lbn r15 Exits HASH("router-exit-3") ReferenceId Sum 
push r15
lbn r15 Exits HASH("router-exit-4") ReferenceId Sum 
push r15
move Address sp

define MAXxROUTE 4

move OldRoute INIT
s Router Setting INIT

sbn HASH("StructureLogicMemory") HASH("store-mutex") Setting 0

move SelectedId -1
jal resetOther

main:
    yield
    l Route Router Setting
    bltz Route main #If route is low 0 continue
    beq Route OldRoute main #If route is not chnaged do nothing
    move OldRoute Route
    bgt Route MAXxROUTE main #In case of incorrect route pass through
    get SelectedId Router Route #Read exit hash from the memory
    breqz SelectedId 2
        s SelectedId Mode EXTRACT 
    j resetOther
j main

resetOther:
    move sp Address
routesCycle:
    pop r15
    beqz r15 main
    breq r15 SelectedId 2 #Selected address has been already set
        s r15 Mode PASS
    s r15 Setting 0
    s r15 SettingOutput 0
    s r15 Quantity 0
j routesCycle