#terain-mars\energy-control\console\power-input-displays.ic10
#This script is reponsible for power output display the nerergy balance
#From power sources, batteries and consumption
define Solar HASH("StructureSolarPanelDual")
define SOLARxGAUGExSETUPxTICKS 100 #Timeout for set gauge scale
alias CounterSolar r1
#init display colors
s $51306 Mode DisplayMode.Power #SOLARxINCOMExDISPLAY
s $5130E Mode DisplayMode.Power #SMALLxINPUTxDISPLAY
s $5130E Color Color.Green #SMALLxINPUTxDISPLAY
s $51325 Mode DisplayMode.Power #SMALLxOUTPUTxDISPLAY
s $51325 Color Color.Yellow #SMALLxOUTPUTxDISPLAY
s $519B9 Mode DisplayMode.Power #SMALLxCAPACITYxDISPLAY
s $519B9 Color Color.Blue #SMALLxCAPACITYxDISPLAY
s $5AC74 Mode DisplayMode.Power #SMALLxITEGRALxCURRENTxDISPLAY
s $5AC75 Mode DisplayMode.Power #SMALLxITEGRALxxDISPLAY
s $5AC74 Color Color.Gray
s $5AC75 Color Color.Gray
s $5AC76 Color Color.Gray
s $5B2B2 Mode DisplayMode.Seconds #Cycle
s $5C63D Mode DisplayMode.Power #Balance per tick
#Setup solar gauge
move CounterSolar SOLARxGAUGExSETUPxTICKS #First time do gauge calibration
#Init bataeis data
alias LastIputState r2
alias CurrentIntegral r3
alias StoredIntegral r4
alias Seconds r5
alias BalancePerTicks r6
move LastIputState 0
move CurrentIntegral 0
move StoredIntegral 0
move Seconds 0
define SMBxCONSUMPTION 50# Each battery takes  50

main:
    jal displaySolar
    jal displayBattaries
    add Seconds Seconds 0.5
    yield
j main

displaySolar:
    brlt CounterSolar SOLARxGAUGExSETUPxTICKS 4
        lb r15 Solar Maximum Sum
        s $51298 Mode r15 #SOLARxINPUTxGAUGE
        move CounterSolar 0
    add CounterSolar CounterSolar 1
    lb r15 Solar Charge Sum
    s $51306 Setting r15 #SOLARxINCOMExDISPLAY
    s $51298 Setting r15 #SOLARxINPUTxGAUGE
    lb r15 Solar Ratio Average
    s $51305 Setting r15 #SOLARxRATIOxINDICATOR
    push ra
    move r14 $51298 #SOLARxINPUTxGAUGE
    jal setColorbyRatio
    move r14 $51305 #SOLARxRATIOxINDICATOR
    jal setColorbyRatio
    pop ra
j ra

displayBattaries:
    lbn r15 HASH("StructureBattery") HASH("bataries-pool-1") Charge Sum
    move r14 $519B9 #capacity display id
    s r14 Setting r15 #Set capacity display
    lbn r15 HASH("StructureBattery") HASH("bataries-pool-1") Ratio Average
    #Setup capacity display color
    push ra
    jal setColorbyRatio
    pop ra
    #Define self consumption
    lbn r13 HASH("StructureBattery") HASH("bataries-pool-1") On Sum
    mul r13 r13 SMBxCONSUMPTION
    l r15 $5134F PowerActual #Input power
    s $5130E Setting r15 #Set input display
    l r14 $5163F PowerActual #Output power
    s $51325 Setting r14 #Set output display
    #Calculate integral
    add CurrentIntegral CurrentIntegral r15 #Add input power
    sub CurrentIntegral CurrentIntegral r14 #Subtract output power
    sub CurrentIntegral CurrentIntegral r13 #Self consumption
    bnez LastIputState nointegralperiodchange #Skip if previous input was On
    beqz r15 nointegralperiodchange #Skip if previous input is Off and current also Off
        move StoredIntegral CurrentIntegral #Reset integral if power just turned On
        move CurrentIntegral 0 #Reset current integral
        s $5AC75 Setting StoredIntegral #Set stored integral display
        s $5B2B2 Setting Seconds
        div BalancePerTicks StoredIntegral Seconds
        div BalancePerTicks BalancePerTicks 2
        s $5C63D Setting BalancePerTicks #Set balance per tick
        move Seconds 0
        move r10 Color.Green
        brgtz StoredIntegral 6
        breqz StoredIntegral 3
            move r10 Color.Red
            jr 4
            move r10 Color.Green
            jr 2
            move r10 Color.Blue
        s $5AC76 Color r10 #Set gauge collor
        s $5AC75 Color r10 #Set integral display color
        s $5C63D Color r10 #Set balance per tick
        add BalancePerTicks BalancePerTicks 1000
        brle BalancePerTicks 2000 2
            move BalancePerTicks 2000
        brgez BalancePerTicks 2
            move BalancePerTicks 0
        div BalancePerTicks BalancePerTicks 2000
        s $5AC76 Setting BalancePerTicks
nointegralperiodchange:
    move LastIputState r15  #Store input state
    s $5AC74 Setting CurrentIntegral #Show current integra
j ra

#r15 - ratio, #r14 device id
setColorbyRatio:
    brle r15 0.9 3
        s r14 Color Color.Blue #SMALLxCAPACITYxDISPLAY
        j ra
    brle r15 0.3 3
        s r14 Color Color.Green #SMALLxCAPACITYxDISPLAY
        j ra
    breqz r15 3
        s r14 Color Color.Yellow #SMALLxCAPACITYxDISPLAY
        j ra
    s r14 Color Color.Gray #SMALLxCAPACITYxDISPLAY
j ra