#terain-mars\energy-control\console\reserved-balance-console.ic10
#This script is reponsible for power output display the nerergy balance
#From power sources, batteries and consumption
define Solar HASH("StructureSolarPanelDual")
define SOLARxGAUGExSETUPxTICKS 100 #Timeout for set gauge scale
alias CounterSolar r10
#init display colors
s $51306 Mode DisplayMode.Power #SOLARxINCOMExDISPLAY
s $5130E Mode DisplayMode.Power #SMALLxINPUTxDISPLAY
s $5130E Color Color.Green #SMALLxINPUTxDISPLAY
s $51325 Mode DisplayMode.Power #SMALLxOUTPUTxDISPLAY
s $51325 Color Color.Yellow #SMALLxOUTPUTxDISPLAY
s $519B9 Mode DisplayMode.Power #SMALLxCAPACITYxDISPLAY
s $519B9 Color Color.Blue #SMALLxCAPACITYxDISPLAY
s $5AC74 Mode DisplayMode.Power #SMALLxITEGRALxCURRENTxDISPLAY
s $5AC75 Mode DisplayMode.Power #SMALLxITEGRALxxDISPLAY
s $5AC74 Color Color.Gray
s $5AC75 Color Color.Gray
s $5AC76 Color Color.Gray
s $5B2B2 Mode DisplayMode.Minutes #Cycle
s $5C63D Mode DisplayMode.Power #Balance per tick
#Setup solar gauge
move CounterSolar SOLARxGAUGExSETUPxTICKS #First time do gauge calibration
#Init bataeis data
alias Balance r0
alias LightSensor r1
alias TimeCounter r2
alias BalanceCurrent r3
alias BalanceStored r4
alias LastLightState r5
alias BalancePerTick r6
alias Seconds r7
move LightSensor $27171
move TimeCounter $88DB3
move BalanceCurrent $88DB4
move BalanceStored $88DB5
s TimeCounter Setting 0
s BalanceCurrent Setting 0
s BalanceStored Setting 0
move LastLightState 0
move Seconds 0
define SMBxCONSUMPTION 50# Each battery takes  50
main:
    yield
    jal timerTick
    jal displaySolar
    jal displayBattaries
    s TimeCounter Setting Seconds
j main
timerTick:
    l Seconds TimeCounter Setting
    add Seconds Seconds 0.5
j ra
displayBattaries:
    lbn r15 HASH("StructureBattery") HASH("bataries-pool-1") Charge Sum
    move r14 $519B9 #capacity display id
    s r14 Setting r15 #Set capacity display
    lbn r15 HASH("StructureBattery") HASH("bataries-pool-1") Ratio Average
    #Setup capacity display color
    push ra
    jal setColorbyRatio
    pop ra
    #Define self consumption
    lbn r13 HASH("StructureBattery") HASH("bataries-pool-1") On Sum
    mul r13 r13 SMBxCONSUMPTION
    l r15 $5134F PowerPotential #Input power
    s $5130E Setting r15 #Set input display
    l r14 $5163F PowerActual #Output power
    s $51325 Setting r14 #Set output display
    #Calculate integral
    l Balance BalanceCurrent Setting
    add Balance Balance r15
    sub Balance Balance r14
    sub Balance Balance r13
    l r15 LightSensor Activate
    beq r15 LastLightState sameCalulationPeriod
    move LastLightState r15
    bne LastLightState 1 sameCalulationPeriod
        s BalanceStored Setting Balance
        s $5AC75 Setting Balance #Set stored integral display
        div r15 Seconds 60
        ceil r15 r15
        s $5B2B2 Setting r15
        div BalancePerTick Balance Seconds
        s $5C63D Setting BalancePerTick #Set balance per tick
        move Seconds 0
        sgez r15 Balance
        move Balance 0
        select r15 r15 Color.Blue Color.Red
        s $5AC76 Color r15 #Set gauge collor
        s $5AC75 Color r15 #Set integral display color
        s $5C63D Color r15 #Set balance per tick
        add BalancePerTick BalancePerTick 1000
        brle BalancePerTick 2000 2
            move BalancePerTick 2000
        brgez BalancePerTick 2
            move BalancePerTick 0
        div BalancePerTick BalancePerTick 2000
        s $5AC76 Setting BalancePerTick
sameCalulationPeriod:
    s $5AC74 Setting Balance #Show current integral
    s BalanceCurrent Setting Balance
j ra  
#r15 - ratio, #r14 device id
setColorbyRatio:
    brle r15 0.9 3
        s r14 Color Color.Blue #SMALLxCAPACITYxDISPLAY
        j ra
    brle r15 0.3 3
        s r14 Color Color.Green #SMALLxCAPACITYxDISPLAY
        j ra
    breqz r15 3
        s r14 Color Color.Yellow #SMALLxCAPACITYxDISPLAY
        j ra
    s r14 Color Color.Gray #SMALLxCAPACITYxDISPLAY
j ra
displaySolar:
    brlt CounterSolar SOLARxGAUGExSETUPxTICKS 4
        lb r15 Solar Maximum Sum
        s $51298 Mode r15 #SOLARxINPUTxGAUGE
        move CounterSolar 0
    add CounterSolar CounterSolar 1
    lb r15 Solar Charge Sum
    s $51306 Setting r15 #SOLARxINCOMExDISPLAY
    s $51298 Setting r15 #SOLARxINPUTxGAUGE
    lb r15 Solar Ratio Average
    s $51305 Setting r15 #SOLARxRATIOxINDICATOR
    push ra
    move r14 $51298 #SOLARxINPUTxGAUGE
    jal setColorbyRatio
    move r14 $51305 #SOLARxRATIOxINDICATOR
    jal setColorbyRatio
    pop ra
j ra