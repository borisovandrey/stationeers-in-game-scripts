#terain-mars\energy-control\solar-panel-control\follow-sun-calibration.ic10
#Script intended to follow solar position with solar pannel
#It has 3 modes:
# 1. Night parking (in position of previous sunraise)
# 2. Solar follow at daylight
# 3. Calibration on first start (wait untill day begins) to adjust positions of sensor and batteries
# Note: All solar pannels shall be installed orienting in the same direction
# Note: Sensor is assumed in a horisontal position
define Solar HASH("StructureSolarPanelDual")
define SOLARxTURNxMAX 40 #seconds as maximal turn time 

alias Sensor d0 #Daylight sensor

alias ParkingPositionHor r1
alias AdjastmentHor r2

move ParkingPositionHor 0
move AdjastmentHor 0
l r15 Sensor Activate
beqzal r15 NightParking
jal Calibrate

main:
    l r15 Sensor Activate
    beqzal r15 NightParking
    l r15 Sensor Horizontal
    add r15 r15 AdjastmentHor
    jal Normalize
    sb Solar Horizontal r15
    l r15 Sensor Vertical
    sub r15 90 r15
    sb Solar Vertical r15
    yield
j main

Calibrate:
    alias MaxPower r14
    alias CurrentAdjustment r13
    move MaxPower 0
    move CurrentAdjustment 0
    sb Solar Vertical 45
    sleep 2
    CalibCycleStart:
        l r15 Sensor Activate
        beqz r15 Calibrate
        l r15 Sensor Horizontal
        add r15 r15 CurrentAdjustment
        push ra
        jal Normalize
        pop ra
        sb Solar Horizontal r15
        sleep SOLARxTURNxMAX #We can't take real solar panel position, so wait maximal time for turn
        lb r15 Solar Charge Average
        ble r15 MaxPower CalibCycleLess
            move MaxPower r15
            move AdjastmentHor CurrentAdjustment
        CalibCycleLess:
        add CurrentAdjustment CurrentAdjustment 90
    blt CurrentAdjustment 360 CalibCycleStart
j ra

NightParking:
    add r15 ParkingPositionHor AdjastmentHor
    push ra
    jal Normalize
    pop ra
    sb Solar Horizontal r15
    sb Solar Vertical 0
    sleep 5
        l r15 Sensor Activate
    breqz r15 -2
    l ParkingPositionHor Sensor Horizontal
j ra

Normalize:
    brgez r15 3
        add r15 r15 360
    jr -2
    brlt r15 360 3
        sub r15 r15 360
    jr -2
j ra