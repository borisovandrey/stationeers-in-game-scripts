alias state r0
alias spAddr r1
alias tgtval r2
alias previousdeviation r3
alias integral r4
alias currentVal r5
alias furnIngotReady r6
alias requiredIngot r7
alias swOn r8
alias output r9
alias deviation r10
alias outputTemp r11
alias outputPrss r12
alias LED d0
alias hotInput d1
alias coolInput d2
alias switch d3
alias tempTgt d4
alias prssTgt d5
define FURNANCE 545937711
define PRESSURExMAX 50000
define TEMPERATURExADDR 10
define PRESSURExADDR 30
define Kp 1.5
define Ki 0.5
define Kd 0.2
define dt 0.5
define Klvn 273.15
define AGINGxERRORxTHRESHOLD 0.15
define AGINGxLOWxERROR 0.5
define AGINGxHIGHxERROR 0.8
define LOWxPATHxFILTERxALPHA 0.2
define STATExIDLE 0
define STATExACTIVE 1
define CONTROLLERxTOLLERANCE 3.5
s hotInput Setting 0
s coolInput Setting 0
s hotInput On 1
s coolInput On 1
sb FURNANCE On 1
move state STATExIDLE
move previousdeviation 0
move integral 0
move tgtval 0
move spAddr TEMPERATURExADDR
jal storeLocal
move spAddr PRESSURExADDR
jal storeLocal
main:
sleep dt
lb furnIngotReady FURNANCE RecipeHash Minimum
l requiredIngot db Setting
seq furnIngotReady requiredIngot furnIngotReady
l swOn switch Setting
brnez swOn 3
s LED Color 1
jr 2
breqz furnIngotReady 2
s LED Color 2
xor furnIngotReady furnIngotReady 1
and currentVal swOn furnIngotReady
bneal currentVal state onChangeState
beqz state main
move output 0
move spAddr TEMPERATURExADDR
jal loadLocal
lb currentVal FURNANCE Temperature Minimum
sub r15 tgtval currentVal
div deviation r15 tgtval
jal pidController
move outputTemp output
jal storeLocal
move spAddr PRESSURExADDR
jal loadLocal
lb currentVal FURNANCE Pressure Minimum
brle currentVal PRESSURExMAX 4
move currentVal STATExIDLE
jal onChangeState
j main
sub r15 tgtval currentVal
div deviation r15 tgtval
jal pidController
move outputPrss output
jal storeLocal
jal applyActuators
j main
jr 75
onChangeState:
move state currentVal
clr db
breqz state 9
sb FURNANCE SettingInput 100
l currentVal tempTgt Setting
move sp TEMPERATURExADDR
push currentVal
l currentVal prssTgt Setting
move sp PRESSURExADDR
push currentVal
j ra
sb FURNANCE SettingInput 0
sb FURNANCE SettingOutput 0
s hotInput Setting 0
s coolInput Setting 0
j ra
storeLocal:
move sp spAddr
push tgtval
push previousdeviation
push integral
j ra
loadLocal:
add sp spAddr 3
pop integral
pop previousdeviation
pop tgtval
j ra
pidController:
abs currentVal deviation
sge currentVal currentVal AGINGxERRORxTHRESHOLD
select currentVal currentVal AGINGxHIGHxERROR AGINGxLOWxERROR
mul r15 integral currentVal
mul r14 deviation dt
add integral r15 r14
min integral integral 1
max integral integral -1
mul r15 deviation LOWxPATHxFILTERxALPHA
sub r14 1 LOWxPATHxFILTERxALPHA
mul r14 r14 previousdeviation
add previousdeviation r15 r14
mul r15 Kp deviation
mul r14 Ki integral
add r15 r15 r14
sub r14 deviation previousdeviation
div r14 r14 dt
mul r14 Kd r14
add output r15 r14
move previousdeviation deviation
min output output 1
max output output -1
mul output output 100
j ra
applyActuators:
abs output outputPrss
brgez outputPrss 4
sb FURNANCE SettingOutput output
move outputPrss 0
jr 2
sb FURNANCE SettingOutput 0
abs output outputTemp
max output output outputPrss
brltz outputTemp 5
s LED Color 4
s hotInput Setting output
s coolInput Setting 0
jr 4
s LED Color -1
s hotInput Setting 0
s coolInput Setting output
brgt output CONTROLLERxTOLLERANCE 2
s LED Color 5
j ra