s d1 Setting 0
s d2 Setting 0
move r0 0
move r3 0
move r4 0
move r2 0
move r1 10
jal storeLocal
move r1 30
jal storeLocal
main:
    sleep 0.5
    lb r6 HASH("StructureAdvancedFurnace") RecipeHash Minimum
    l r7 db Setting #read required hash
    seq r6 r7 r6 #Check we already have this hash done
    l r8 d3 Setting #Check auto switch is on
    l r15 d0 Color #Define current color
    select r15 r6 Color.Green r15 #Green lamp if ingot is ready
    select r15 r8 r15 Color.Gray #Gray color if switched off 
    s d0 Color r15  
    xor r6 r6 1 # !HashReady
    and r5 r8 r6 # Active state is sw=On && !HashReady 
    bneal r5 r0 onChangeState #State is changed set everthing up
beqz r0 main #Current state is IDLE - do nothing
    move r9 0 #Calculate temperature error
    move r1 10 #Temperature address
    jal loadLocal #Load data
    lb r5 HASH("StructureAdvancedFurnace") Temperature Minimum
    sub r15 r2 r5
    div r10 r15 r2 #Calculate normilized error [-1..1]
    jal pidController #Apply calculation for temperature
    move r11 r9  #Temperature controller output [-10.0..10.0]
    jal storeLocal 
    move r1 30 #Calculate pressure error
    jal loadLocal
    lb r5 HASH("StructureAdvancedFurnace") Pressure Minimum
    brle r5 50000 4 #Protect the pressure
        move r5 0
        jal onChangeState
        j main
    sub r15 r2 r5
    div r10 r15 r2
    jal pidController
    move r12 r9
    jal storeLocal
    jal applyActuators #Apply controllers
j main
onChangeState:
    move r0 r5
    clr db
    breqz r0 12 #In case of state is not IDLE
        sbn HASH("StructureDigitalValve") HASH("fa-inj-auto-sw") On 0
        sb HASH("StructureAdvancedFurnace") SettingInput 100
        s d1 On 1 #Hot input
        s d2 On 1 #Cool input
        l r5 d4 Setting #Temp target
        move r16 10
        push r5
        l r5 d5 Setting #Pressure target
        move r16 30
        push r5
    j ra #State is IDLE
        sb HASH("StructureAdvancedFurnace") SettingInput 0
        sb HASH("StructureAdvancedFurnace") SettingOutput 0
        s d1 Setting 0  #Hot input
        s d2 Setting 0  #Cool input
        s d1 On 0
        s d2 On 0
        sbn HASH("StructureDigitalValve") HASH("fa-inj-auto-sw") On 1
j ra
storeLocal:
    move r16 r1
    push r2
    push r3
    push r4
j ra
loadLocal:
    add r16 r1 3
    pop r4
    pop r3
    pop r2
j ra
pidController:
    abs r5 r10
    sge r5 r5 0.15
    select r5 r5 0.8 0.5
    mul r15 r4 r5
    mul r14 r10 0.5
    add r4 r15 r14
    min r4 r4 1
    max r4 r4 -1
    mul r15 r10 0.2
    mul r14 0.8 r3
    add r3 r15 r14
    mul r15 1.5 r10
    mul r14 0.5 r4
    add r15 r15 r14
    sub r14 r10 r3
    div r14 r14 0.5
    mul r14 0.2 r14
    add r9 r15 r14
    move r3 r10
    min r9 r9 1
    max r9 r9 -1
    mul r9 r9 10
j ra
applyActuators:
    abs r9 r12 #Pressure
    brgez r12 5 #In case of negative deviation by pressure open the furnance output
        mul r9 r9 10 #Furnance output control has range [0..100]
        sb HASH("StructureAdvancedFurnace") SettingOutput r9
        move r12 0
    jr 2
        sb HASH("StructureAdvancedFurnace") SettingOutput 0
    abs r9 r11
    max r9 r9 r12
    brltz r11 5
        s d0 Color Color.Red
        s d1 Setting r9
        s d2 Setting 0
    jr 4
        s d0 Color Color.Blue
        s d1 Setting 0
        s d2 Setting r9
    brgt r9 0.3 2 #In cas of low output value consider we are on the target
        s d0 Color Color.Orange
j ra